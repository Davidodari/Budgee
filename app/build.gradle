apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion sdkVersion
    defaultConfig {
        applicationId "com.odaroid.budgee"
        minSdkVersion minimumSdkVersion
        targetSdkVersion sdkVersion
        versionCode generateVersionCode()
        versionName generateVersionName("$versionMajor.$versionMinor.$versionPatch")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled true
    }
}

dependencies {
    def room_version = "2.1.0-rc01"
    def navigation_version = "2.0.0"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0-alpha07'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

//    Testing
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation('androidx.test.ext:junit:1.1.1') {
        exclude group: 'androidx.test', module: 'core'
    }
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'org.mockito:mockito-android:2.28.2'
    debugImplementation('androidx.fragment:fragment-testing:1.1.0-beta01') {
        exclude group: 'androidx.test', module: 'core'
    }

//    Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

//    Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"
}

private Integer generateVersionCode() {
    return (minimumSdkVersion * 10000000) + (versionMajor * 10000) + (versionMinor * 100) + versionPatch
}

private String generateVersionName(String versionName) {
    if (versionClassifier != null) {
        versionName += "-$versionClassifier"
    }
    return versionName
}
